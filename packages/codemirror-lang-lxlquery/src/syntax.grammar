@top Query { term* }

@skip { space }

term {
  freetext |
  Qualifier |
  Group |
  BooleanQuery |
  NegativeQuery |
  DoubleNegative
}

Group { "(" term* ")" } 

BooleanQuery { 
  (freetext | Qualifier | Group | NegativeQuery) BooleanOperator (freetext | Qualifier | Group | NegativeQuery) 
  (BooleanOperator (freetext | Qualifier | Group | NegativeQuery))+?
}

NegativeQuery { 
  NotOperator (freetext | Qualifier | Group) 
}

DoubleNegative { NotOperator NotOperator }

Qualifier {
  (QualifierKey QualifierOperator QualifierValue) | reserved
}

QualifierKey {
  Identifier | String
}

QualifierValue {
  Identifier Wildcard? | String | Number Wildcard? | Group | Wildcard
}

QualifierOperator {
  EqualOperator | CompareOperator
}

freetext {
  (Identifier | String | Number) Wildcard?
}

@tokens {
  Identifier { $[a-zA-ZåäöÅÄÖ]+ }

  String { '"' (!["\\] | "\\" _)* '"' }

  Number { @digit+ }

  EqualOperator { ":" | "=" }

  CompareOperator { ">" | "<" | ">=" | "<=" }

  BooleanOperator { "AND" | "OR" }

  NotOperator { "NOT" }

  Wildcard { "*" }

  reserved { "includeEplikt" | "includePreliminary" }

  space { @whitespace+ }

  @precedence { BooleanOperator, NotOperator, reserved, Identifier }
}

@detectDelim