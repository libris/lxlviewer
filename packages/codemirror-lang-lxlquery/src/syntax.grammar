@top Query { term* }

@skip { space }

term {
  freetext |
  Qualifier |
  Group |
  BooleanQuery
}

Group { "(" term* ")" } 

BooleanQuery { 
  (freetext | Qualifier | Group ) BooleanOperator (freetext | Qualifier | Group ) 
  (BooleanOperator (freetext | Qualifier | Group ))+?
}

Qualifier {
  (QualifierKey QualifierOperator QualifierValue) | reserved
}

QualifierKey {
  Identifier | String
}

QualifierValue {
  Identifier Wildcard? | String | Number | Group | Wildcard
}

QualifierOperator {
  EqualOperator | CompareOperator
}

freetext {
  (Identifier | String | Number) Wildcard?
}

@tokens {
  Identifier { $[a-zA-ZåäöÅÄÖ]+ }

  String { '"' (!["\\] | "\\" _)* '"' }

  Number { @digit+ }

  EqualOperator { ":" | "=" }

  CompareOperator { ">" | "<" | ">=" | "<=" }

  BooleanOperator { "AND" | "OR" | "NOT" }

  Wildcard { "*" }

  reserved { "includeEplikt" | "includePreliminary" }

  space { @whitespace+ }

  @precedence { BooleanOperator, reserved, Identifier }
}

@detectDelim